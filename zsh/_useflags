#compdef useflags
# (C) Martin V\"ath <martin at mvath.de>
# SPDX-License-Identifier: BSD-3-Clause
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
_arguments -C -s -S -A '-*' : \
'(1 2 3 -)'{'--help','-h'}'[print a brief help]' \
'(1 2 3 -)'{'--man','-\\?'}'[show extended help as a manpage]' \
{'(--version)-V','(-V)--version'}'[print version and exit]' \
{'(--verbose)-v','(-v)--verbose'}'[verbose (default)]' \
{'(--brief)-b','(-b)--brief'}'[brief\: only deal with effective state of flags]' \
{'(--reverse)-r','(-r)--reverse'}'[swap meaning of old/new]' \
{'(--file)-f+','(-f)--file='}'[SAVEPATH. Use SAVEPATH instead of /etc/portage/useflags]:savepath:_files' \
'1:command:->cmds' \
'2::file:_files' \
'3::file2:_files'
local ret=$?
case $state in
cmds)
	local cmds
	cmds=(
	'save:save useflags to /etc/portage/useflags or FILE'
	'diff:diff useflags with /etc/portage/useflags or FILE(s)'
	'print:print current useflags'
	'cat:print useflags from /etc/portage/useflags or FILE'
	'diff64:diff useflags from /gentoo{32,64}/etc/portage/useflags'
	'help:print brief help'
	'man:print verbose manpage'
	)
	_describe -t commands 'command' cmds && ret=0;;
esac
return ret
